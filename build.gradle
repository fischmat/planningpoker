import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.CsvReportRenderer
import com.github.jk1.license.render.InventoryHtmlReportRenderer
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'
	id 'org.jetbrains.kotlin.jvm' version '1.9.0'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.0'
	id 'jacoco'
	id 'com.github.jk1.dependency-license-report' version '2.5'
}

group = 'de.matthiasfisch'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	def scrimageVersion = "4.0.38"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-websocket"
	implementation "org.springframework.boot:spring-boot-starter-graphql"
	implementation "org.springframework.security:spring-security-crypto"
	implementation "org.springframework.boot:spring-boot-starter-jdbc"
	implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
	implementation "org.jetbrains.kotlin:kotlin-reflect"
	implementation "org.springframework.session:spring-session-core"
	implementation "io.github.microutils:kotlin-logging-jvm:3.0.5"
	implementation "io.minio:minio:8.5.4"
	implementation "com.sksamuel.scrimage:scrimage-core:${scrimageVersion}"
	implementation "com.sksamuel.scrimage:scrimage-formats-extra:${scrimageVersion}"
	implementation "com.sksamuel.scrimage:scrimage-webp:${scrimageVersion}"
	implementation "commons-codec:commons-codec:1.16.0"
	implementation "org.jooq:jooq:3.18.6"
	implementation "org.postgresql:postgresql:42.6.0"
	implementation "org.flywaydb:flyway-core:9.22.2"
	developmentOnly "org.springframework.boot:spring-boot-devtools"
	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		// JSONObject clashes with the one from org.json:json through io.socket:socket.io-client
		exclude group: 'com.vaadin.external.google', module: 'android-json'
	}

	implementation platform("org.http4k:http4k-bom:4.43.0.0")
	implementation "org.http4k:http4k-core"
	implementation "org.http4k:http4k-client-apache"

	def kotestVersion = "5.6.2"
	testImplementation "io.kotest:kotest-runner-junit5:${kotestVersion}"
	testImplementation "io.kotest:kotest-assertions-core:${kotestVersion}"
	testImplementation "io.kotest.extensions:kotest-extensions-spring:1.1.3"
	testImplementation "io.rest-assured:rest-assured:5.3.1"
	testImplementation "io.socket:socket.io-client:1.0.2"
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '17'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}



licenseReport {
	renderers = [new InventoryHtmlReportRenderer(), new CsvReportRenderer()]
	excludeBoms = true
	allowedLicensesFile = new File("$projectDir/config/allowed-licenses.json")
	filters = [new LicenseBundleNormalizer()]
}
build {
	dependsOn checkLicense
}